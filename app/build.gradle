plugins {
    id("com.android.application")
    id("kotlin-android")
    id("kotlin-android-extensions")
    id("com.github.triplet.play") version "2.6.2"
    id("com.google.gms.google-services")
    id("androidx.navigation.safeargs.kotlin")
}

apply from: '../release_notes_generator.gradle'

android {

    compileSdkVersion 30

    defaultConfig {
        applicationId "nl.jovmit.lyrics"
        minSdkVersion 24
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "nl.jovmit.lyrics.TestAppRunner"
    }

    signingConfigs {
        release {
            storeFile file(System.getenv("HOME") + "/keystores/jovmit")
            storePassword System.getenv("BITRISEIO_ANDROID_KEYSTORE_PASSWORD")
            keyAlias System.getenv("BITRISEIO_ANDROID_KEYSTORE_ALIAS")
            keyPassword System.getenv("BITRISEIO_ANDROID_KEYSTORE_PRIVATE_KEY_PASSWORD")
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    viewBinding {
        enabled = true
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"
    implementation "com.google.android.material:material:1.2.1"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:$swipeRefreshLayoutVersion"
    implementation "androidx.activity:activity-ktx:$activityKtxVersion"
    implementation "androidx.fragment:fragment-ktx:$fragmentKtxVersion"
    implementation "androidx.core:core-ktx:1.3.2"
    implementation "androidx.constraintlayout:constraintlayout:2.0.4"
    implementation "org.koin:koin-android:$koinVersion"
    implementation "org.koin:koin-androidx-viewmodel:$koinVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutinesVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutinesVersion"
    implementation "androidx.navigation:navigation-fragment-ktx:$androidArchNavigationVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$androidArchNavigationVersion"
    implementation 'com.google.firebase:firebase-firestore:22.0.2'

    androidTestImplementation "androidx.test.ext:junit:1.1.2"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.3.0"
    androidTestImplementation "androidx.test:rules:1.3.0"

    testImplementation "org.junit.jupiter:junit-jupiter-api:5.7.0"
    testImplementation "org.junit.jupiter:junit-jupiter-params:5.7.0"
    testImplementation "org.koin:koin-test:$koinVersion"
    testImplementation "androidx.arch.core:core-testing:2.1.0"
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:$mockitoKotlinVersion"
    testImplementation("org.mockito:mockito-junit-jupiter:3.6.28")

    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.7.0"
}

play {
    serviceAccountCredentials = file(System.getenv("HOME") + "/keystores/google_services_api.json")
    defaultToAppBundles = true
    track = "alpha" //[internal/alpha/beta/production]
    releaseStatus = "completed" //[completed/draft/inProgress/halted]
//    userFraction = 0.2 // [1 = 100%]
}
